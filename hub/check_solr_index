#!/usr/bin/perl
#
# Check the number of Solr index items. Including:
# - MCI
# - LCX
#
# Usage: check_solr_index [options]
#
# Options:
#  -e | --envid             Specify the environment ID to check. eg, srwd83.
#     | --mci-listing       Specify the expect number for listing on MCI.
#     | --mci-order         Specify the expect number for order on MCI.
#     | --mci-payment       Specify the expect number for payment on MCI.
#     | --lcx-genre         Specify the expect number for genre on LCX.
#     | --lcx-cobrand       Specify the expect number for cobrand on LCX.
#     | --lcx-event         Specify the expect number for event on LCX.
#     | --lcx-ticket        Specify the expect number for ticket on LCX.
#  -v | --verbose           Show verbose messages.
#  -h | --help              Show help information.
#
# Examples:
#  check_solr_index -e srwd83
#  check_solr_index -e srwd83 --mci-listing 10
#  check_solr_index -e srwd83 --mci-payment 0 --lcx-ticket 0
#
# Author: minjzhang
#

use strict;
use warnings;
use Carp;
use Getopt::Long;

use lib '/nas/reg/lib/perl';
use lib '/nas/utl/devops/lib/perl';

use Readonly;
use Stubhub::Log::Util qw (
                            init
                            get_logger_with_loglevel
                        );

# Get options
my $show_usage = qw{};
my $show_verbose = qw{};
my $envid = qw{};

my $mci_listing_expected = 1;
my $mci_order_expected = 1;
my $mci_payment_expected = 1;
my $lcx_genre_expected = 35000;
my $lcx_cobrand_expected = 2200;
my $lcx_event_expected = 60000;
my $lcx_ticket_expected = 10000;

my $options_okay = GetOptions (
    'e|envid=s'     => \$envid,
    'mci-listing=s' => \$mci_listing_expected,
    'mci-order=s'   => \$mci_order_expected,
    'mci-payment=s' => \$mci_payment_expected,
    'lcx-genre=s'   => \$lcx_genre_expected,
    'lcx-cobrand=s' => \$lcx_cobrand_expected,
    'lcx-event=s'   => \$lcx_event_expected,
    'lcx-ticket=s'  => \$lcx_ticket_expected,
    'v|verbose'     => \$show_verbose,
    'h|help'        => \$show_usage,
);

#
# Initiate log instance
#
Stubhub::Log::Util->init();
our $logger = get_logger_with_loglevel( $show_verbose );

#
# Signal Handler
#
$SIG{'INT'} = \&sigIntHandler;

#
# Clean up and exit when catch SIGINT(2)
#
sub sigIntHandler {
    exit;
}

if ( $show_usage ) {
    usage();
    exit 0;
}

#
# Usage
#
sub usage {
    print <<END_OF_HELP
Check the number of Solr index items. Including:
- MCI
- LCX

Usage: check_solr_index [options]

Options:
 -e | --envid             Specify the environment ID to check. Eg, swd83.
    | --mci-listing       Specify the expect number for listing on MCI.
    | --mci-order         Specify the expect number for order on MCI.
    | --mci-payment       Specify the expect number for payment on MCI.
    | --lcx-genre         Specify the expect number for genre on LCX.
    | --lcx-cobrand       Specify the expect number for cobrand on LCX.
    | --lcx-event         Specify the expect number for event on LCX.
    | --lcx-ticket        Specify the expect number for ticket on LCX.
 -v | --verbose           Show verbose messages.
 -h | --help              Show help information.

Examples:
 check_solr_index -e srwd83
 check_solr_index -e srwd83 --mci-listing 10
 check_solr_index -e srwd83 --mci-payment 0 --lcx-ticket 0

END_OF_HELP
}

#
# Parameter validation.
#
if ( ! defined $envid ) {
    $logger->error( "The pamameter '-e' must be set.\n" );
    usage();
    exit 1;
}

#
# Get the solr instance URL for the specific environment.
#
sub get_solr_instance {
    my ( $envid ) = @_;
    Readonly my $DEV_QA_HOSTS => '/nas/reg/etc/dev-qa-hosts';
    Readonly my $GREP_CMD => '/bin/grep';

    my $mci_hosts = `$GREP_CMD $envid $DEV_QA_HOSTS | $GREP_CMD mci`;
    my @mci_host_list = split "\n", $mci_hosts;
    my @mci_solr = map { local $_ = $_; s/^(.*)$/http:\/\/$1\/solr/; $_ } @mci_host_list;

    my $lcx_hosts = `$GREP_CMD $envid $DEV_QA_HOSTS | $GREP_CMD lcx`;
    my @lcx_host_list = split "\n", $lcx_hosts;
    my @lcx_solr = map { local $_ = $_; s/^(.*)$/http:\/\/$1\/solr/; $_ } @lcx_host_list;

    return( \@mci_solr, \@lcx_solr );
}

#
# Print the query result for specific environment.
#
sub print_query_result {
    my ( $solr_url, $query_result, $expected_result, $result_name ) = @_;
    my $hostname = $solr_url;
    $hostname =~ s/http:\/\/(.*)\/solr.*/$1/;
    my $fail = "";
    $query_result = -1 if $query_result !~ /[0-9]+/;
    $fail = "[fail]" if $query_result < $expected_result;
    printf "%-50s %-8s %-10s\n", "$hostname $result_name:", $query_result, $fail;
    if ( $fail eq "[fail]" ) {
        return 0;
    } else {
        return 1;
    }
}

#
# Get query numFound result.
#
sub query_num_found {
    my ( $solr_query_url ) = @_;

    Readonly my $CURL_CMD => '/usr/bin/curl';
    Readonly my $GREP_CMD => '/bin/grep';

    my $query_result = `$CURL_CMD "$solr_query_url" 2> /dev/null | $GREP_CMD '"numFound"'`;
    chomp $query_result;
    $query_result =~ s/.*"numFound":([0-9]+),.*/$1/;
    return $query_result;
}

#
# Query Solr doc.
#
sub query_solr_index {
    my $success = 1;
    my ( $solr_url, $query_url, $ping_url, $expected_value, $query_name ) = @_;
    if ( check_solr_pingable( $solr_url, $ping_url ) ) {
        my $query_result = query_num_found( "$solr_url/$query_url" );
        $success = 0 if not print_query_result( $solr_url, $query_result, $expected_value, $query_name );
    } else {
        my $hostname = $solr_url;
        $hostname =~ s/http:\/\/(.*)\/solr.*/$1/;
        printf "%-50s %-8s %-10s\n", "$hostname $query_name", "Not pingable", "[fail]";
        $success = 0;
    }
    return $success;
}

#
# Query the listing in MCI.
#
sub query_mci_listing {
    my ( $mci_solr_list_ref, $expected_value ) = @_;
    my $success = 1;
    Readonly my $MCI_LISTING_QUERY => 'inventory/select?q=*%3A*&wt=json&indent=true';
    Readonly my $MCI_LISTING_PING => 'inventory/admin/ping?wt=json';
    foreach my $mci_solr ( @{ $mci_solr_list_ref } ) {
        $success = 0 if not query_solr_index( $mci_solr, $MCI_LISTING_QUERY, $MCI_LISTING_PING, $expected_value, "listing");
    }
    return $success;
}

#
# Query the order in MCI.
#
sub query_mci_order {
    my ( $mci_solr_list_ref, $expected_value ) = @_;
    my $success = 1;
    Readonly my $MCI_ORDERS_QUERY => 'orders/select?q=*%3A*&wt=json&indent=true';
    Readonly my $MCI_ORDERS_PING => 'orders/admin/ping?wt=json';
    foreach my $mci_solr ( @{ $mci_solr_list_ref } ) {
        $success = 0 if not query_solr_index( $mci_solr, $MCI_ORDERS_QUERY, $MCI_ORDERS_PING, $expected_value, "order");
    }
    return $success;
}

#
# Query the payment in MCI.
#
sub query_mci_payment {
    my ( $mci_solr_list_ref, $expected_value ) = @_;
    my $success = 1;
    Readonly my $MCI_PAYMENTS_QUERY => 'payments/select?q=*%3A*&wt=json&indent=true';
    Readonly my $MCI_PAYMENTS_PING => 'payments/admin/ping?wt=json';
    foreach my $mci_solr ( @{ $mci_solr_list_ref } ) {
        $success = 0 if not query_solr_index( $mci_solr, $MCI_PAYMENTS_QUERY, $MCI_PAYMENTS_PING, $expected_value, "payment");
    }
    return $success;
}

#
# Query the genre in LCX.
#
sub query_lcx_genre {
    my ( $lcx_solr_list_ref, $expected_value ) = @_;
    my $success = 1;
    my $domain_name = _get_domain_name( @{ $lcx_solr_list_ref }[0] );
    Readonly my $LCX_GENRE_QUERY => 'general/select?q=%2BstubhubDocumentType%3Agenre+%2B(allowedViewingDomain%3A' . $domain_name . '.com+or+allowedViewingDomain%3A' . $domain_name . '.co.uk)&wt=json&indent=true';
    Readonly my $LCX_GENERAL_PING => 'general/admin/ping?wt=json';
    foreach my $lcx_solr ( @{ $lcx_solr_list_ref } ) {
        $success = 0 if not query_solr_index( $lcx_solr, $LCX_GENRE_QUERY, $LCX_GENERAL_PING, $expected_value, "genre");
    }
    return $success;
}

#
# Query the cobrand in LCX.
#
sub query_lcx_cobrand {
    my ( $lcx_solr_list_ref, $expected_value ) = @_;
    my $success = 1;
    my $domain_name = _get_domain_name( @{ $lcx_solr_list_ref }[0] );
    Readonly my $LCX_COBRAND_QUERY => 'general/select?q=%2BstubhubDocumentType%3Acobrand+%2Bdomain%3A.' . $domain_name . '.com&wt=json&indent=true';
    Readonly my $LCX_GENERAL_PING => 'general/admin/ping?wt=json';
    foreach my $lcx_solr ( @{ $lcx_solr_list_ref } ) {
        $success = 0 if not query_solr_index( $lcx_solr, $LCX_COBRAND_QUERY, $LCX_GENERAL_PING, $expected_value, "cobrand");
    }
    return $success;
}

#
# Query the event in LCX.
#
sub query_lcx_event {
    my ( $lcx_solr_list_ref, $expected_value ) = @_;
    my $success = 1;
    my $domain_name = _get_domain_name( @{ $lcx_solr_list_ref }[0] );
    Readonly my $LCX_EVENT_QUERY => 'general/select?q=%2BstubhubDocumentType%3Aevent+%2B(allowedViewingDomain%3A' . $domain_name . '.com+or+allowedViewingDomain%3A' . $domain_name . '.co.uk)&wt=json&indent=true';
    Readonly my $LCX_GENERAL_PING => 'general/admin/ping?wt=json';
    foreach my $lcx_solr ( @{ $lcx_solr_list_ref } ) {
        $success = 0 if not query_solr_index( $lcx_solr, $LCX_EVENT_QUERY, $LCX_GENERAL_PING, $expected_value, "event");
    }
    return $success;
}

#
# Query the ticket in LCX.
#
sub query_lcx_ticket {
    my ( $lcx_solr_list_ref, $expected_value ) = @_;
    my $success = 1;
    Readonly my $LCX_TICKET_QUERY => 'general/select?q=%2BstubhubDocumentType%3Aticket&wt=json&indent=true';
    Readonly my $LCX_GENERAL_PING => 'general/admin/ping?wt=json';
    foreach my $lcx_solr ( @{ $lcx_solr_list_ref } ) {
        $success = 0 if not query_solr_index( $lcx_solr, $LCX_TICKET_QUERY, $LCX_GENERAL_PING, $expected_value, "ticket");
    }
    return $success;
}

#
# Check if mci listing is pingable.
#
sub check_solr_pingable {
    my ( $mci_solr_url, $admin_ping_url ) = @_;
    Readonly my $CURL_CMD => '/usr/bin/curl';
    Readonly my $GREP_CMD => '/bin/grep';

    my $query_result = `$CURL_CMD "$mci_solr_url/$admin_ping_url" 2> /dev/null`;
    if ( $query_result =~ /"status":"OK"/ ) {
        $query_result = 1;
    } else {
        $query_result = 0;
    }
    return $query_result;
}

sub _get_domain_name {
    my ( $domain_name ) = @_;
    chomp $domain_name;
    $domain_name =~ s/.*\.(srw[deq][0-9]+)\..*/$1/;
    return $domain_name;
}

#
# Main.
#

my ( $mci_solr_list_ref, $lcx_solr_list_ref ) = get_solr_instance( $envid );

my $success = 1;

$success = 0 if not query_mci_listing( $mci_solr_list_ref, $mci_listing_expected );
$success = 0 if not query_mci_order( $mci_solr_list_ref, $mci_order_expected );
$success = 0 if not query_mci_payment( $mci_solr_list_ref, $mci_payment_expected );
$success = 0 if not query_lcx_genre( $lcx_solr_list_ref, $lcx_genre_expected );
$success = 0 if not query_lcx_cobrand( $lcx_solr_list_ref, $lcx_cobrand_expected);
$success = 0 if not query_lcx_event( $lcx_solr_list_ref, $lcx_event_expected );
$success = 0 if not query_lcx_ticket( $lcx_solr_list_ref, $lcx_ticket_expected );

exit 0 if $success;
exit 1;
